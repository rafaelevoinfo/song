unit pChatComponentes;

interface

uses
  Winapi.Messages, System.Classes, Vcl.Graphics, Vcl.Controls, Vcl.ExtCtrls,
  Vcl.StdCtrls, Vcl.Buttons, pChatTipos, System.SysUtils, Vcl.ImgList, Vcl.Forms;

type
  TChatImage = class(TImage)
  private
    FOnCMMouseEnter: TNotifyEvent;
    FOnCMMouseLeave: TNotifyEvent;
    procedure CMMouseEnter(var Message: TMessage); message CM_MOUSEENTER;
    procedure CMMouseLeave(var Message: TMessage); message CM_MOUSELEAVE;
    procedure SetOnCMMouseEnter(const Value: TNotifyEvent);
    procedure SetOnCMMouseLeave(const Value: TNotifyEvent);
    { Private declarations }
  public
    constructor Create(AOwner: TComponent); override;
    property OnCMMouseEnter: TNotifyEvent read FOnCMMouseEnter write SetOnCMMouseEnter;
    property OnCMMouseLeave: TNotifyEvent read FOnCMMouseLeave write SetOnCMMouseLeave;
    { Public declarations }
  end;

  TChatButton = class(TButton)
  private
    FCustomForm: TCustomForm;
    FActiveControl: TWinControl;
    procedure CMMouseEnter(var Message: TMessage); message CM_MOUSEENTER;
    procedure CMMouseLeave(var Message: TMessage); message CM_MOUSELEAVE;
    { Private declarations }
  public
    constructor Create(AOwner: TComponent); override;
    { Public declarations }
  published
    procedure MouseUp(Button: TMouseButton; Shift: TShiftState;
      X, Y: Integer); override;
    { Published declarations }
  end;

  TChatPanel = class(TPanel)
  private
    FBotaoFechar: TChatButton;
    FIdentificador: string;
    FImagem: TImage;
    FImgMouseMovePanel: TChatImage;
    FLabelInferior: TLabel;
    FLabelSuperior: TLabel;
    FMouseInControl: Boolean;
    FOnClose: TNotifyEvent;
    FPodeFechar: Boolean;
    function GetFotoThumbnail: TPicture;
    function GetLabelInferior: string;
    function GetLabelSuperior: string;
    procedure ppvFechar(Sender: TObject);
    procedure SetFotoThumbnail(const Value: TPicture);
    procedure SetIdentificador(const Value: string);
    procedure SetLabelInferior(const Value: string);
    procedure SetLabelSuperior(const Value: string);
    procedure SetOnClose(const Value: TNotifyEvent);
    procedure SetPodeFechar(const Value: Boolean);
    procedure WMSize(var Message: TWMSize); message WM_SIZE;
    { Private declarations }
  protected
    procedure pptAjustarPosicaoVerticalComponentes; virtual;
    procedure pptOnClick(Sender: TObject); virtual;
    procedure pptOnCMMouseEnter(Sender: TObject); virtual;
    procedure pptOnCMMouseLeave(Sender: TObject); virtual;
    property MouseInControl: Boolean read FMouseInControl;
    { Protected declarations }
  public
    constructor Create(AOwner: TComponent); override;
    procedure Close;
    procedure ppuImagemBotaoFechar(const ipCustomImageList: TCustomImageList; const ipIndex: Integer);
    property FotoThumbnail: TPicture read GetFotoThumbnail write SetFotoThumbnail;
    property Identificador: string read FIdentificador write SetIdentificador;
    property LabelInferior: string read GetLabelInferior write SetLabelInferior;
    property LabelSuperior: string read GetLabelSuperior write SetLabelSuperior;
    property PodeFechar: Boolean read FPodeFechar write SetPodeFechar;
    property OnClose: TNotifyEvent read FOnClose write SetOnClose;
    { Public declarations }
  end;

  TChatPanelArquivo = class(TChatPanel)
  private
    FEnderecoArquivo: string;
    FTipoArquivo: TChatTipoArquivo;
    procedure SetEnderecoArquivo(const Value: string);
    procedure SetTipoArquivo(const Value: TChatTipoArquivo);
    { Public declarations }
  public
    constructor Create(AOwner: TComponent); override;
    property EnderecoArquivo: string read FEnderecoArquivo write SetEnderecoArquivo;
    property TipoArquivo: TChatTipoArquivo read FTipoArquivo write SetTipoArquivo;
    { Published declarations }
  end;

  TChatPanelConversa = class(TChatPanel)
  private
    FStatus: Integer;
    FShapeStatus: TShape;
    procedure SetStatus(const Value: Integer);
    { Public declarations }
  protected
    procedure pptAjustarPosicaoVerticalComponentes; override;
    { Protected declarations }
  public
    constructor Create(AOwner: TComponent); override;
    property Status: Integer read FStatus write SetStatus;
    { Published declarations }
  end;

  TChatPanelConversaAba = class(TChatPanelConversa)
  private
    FMensagem: TChatMensagem;
    FNotificacao: Boolean;
    FNotificacaoTimer: TTimer;
    FNotificacaoTimerCount: Integer;
    FOnChangeSelect: TNotifyEvent;
    FPodeSelecionar: Boolean;
    FSelecionado: Boolean;
    FTipoAba: TChatTipoAba;
    procedure ppvNotificacaoOnTimer(Sender: TObject);
    procedure SetNotificacao(const Value: Boolean);
    procedure SetOnChangeSelect(const Value: TNotifyEvent);
    procedure SetPodeSelecionar(const Value: Boolean);
    procedure SetSelecionado(const Value: Boolean);
    procedure SetTipoAba(const Value: TChatTipoAba);
    { Public declarations }
  protected
    procedure pptOnCMMouseLeave(Sender: TObject); override;
    { Protected declarations }
  public
    constructor Create(AOwner: TComponent); override;
    destructor Destroy; override;
    procedure Click; override;
    property Mensagem: TChatMensagem read FMensagem;
    property Notificacao: Boolean read FNotificacao write SetNotificacao;
    property OnChangeSelect: TNotifyEvent read FOnChangeSelect write SetOnChangeSelect;
    property PodeSelecionar: Boolean read FPodeSelecionar write SetPodeSelecionar;
    property Selecionado: Boolean read FSelecionado write SetSelecionado;
    property TipoAba: TChatTipoAba read FTipoAba write SetTipoAba;
    { Published declarations }
  end;

implementation

{ TChatImage }

procedure TChatImage.CMMouseEnter(var Message: TMessage);
begin
  inherited;

  if (not (csDesigning in ComponentState)) and Assigned(FOnCMMouseEnter) then
    FOnCMMouseEnter(Self);
end;

procedure TChatImage.CMMouseLeave(var Message: TMessage);
begin
  inherited;

  if (not (csDesigning in ComponentState)) and Assigned(FOnCMMouseLeave) then
    FOnCMMouseLeave(Self);
end;

constructor TChatImage.Create(AOwner: TComponent);
begin
  inherited Create(AOwner);
  FOnCMMouseEnter := nil;
  FOnCMMouseLeave := nil;
end;

procedure TChatImage.SetOnCMMouseEnter(const Value: TNotifyEvent);
begin
  FOnCMMouseEnter := Value;
end;

procedure TChatImage.SetOnCMMouseLeave(const Value: TNotifyEvent);
begin
  FOnCMMouseLeave := Value;
end;

{ TChatButton }

procedure TChatButton.CMMouseEnter(var Message: TMessage);
begin
  inherited;

  if (not (csDesigning in ComponentState)) then
    begin
      Font.Style := [fsBold];
      FCustomForm := GetParentForm(Self);

      if Assigned(FCustomForm) then
        FActiveControl := FCustomForm.ActiveControl;
    end;
end;

procedure TChatButton.CMMouseLeave(var Message: TMessage);
begin
  inherited;

  if (not (csDesigning in ComponentState)) then
    Font.Style := [];
end;

constructor TChatButton.Create(AOwner: TComponent);
begin
  inherited Create(AOwner);
  FCustomForm := nil;
  FActiveControl := nil;
end;

procedure TChatButton.MouseUp(Button: TMouseButton; Shift: TShiftState; X,
  Y: Integer);
begin
  inherited;

  if (not (csDesigning in ComponentState)) and Self.Focused and Assigned(FCustomForm) and Assigned(FActiveControl) and FActiveControl.Showing and FActiveControl.Visible then
    FCustomForm.ActiveControl := FActiveControl;
end;

{ TChatPanel }

procedure TChatPanel.Close;
begin
  if FPodeFechar then
    begin
      if Assigned(FOnClose) then
        FOnClose(Self);

      Self.Free;
    end;
end;

constructor TChatPanel.Create(AOwner: TComponent);
begin
  inherited Create(AOwner);

  // Definindo as características do panel. Este panel será uma a "guia do usuário" na tela
  ShowCaption := False;
  Height := 35;
  ParentBackground := False;

  FMouseInControl := False;
  FOnClose := nil;
  FPodeFechar := True;
  FIdentificador := '';

  // Criando a imagem
  FImagem := TImage.Create(Self);
  FImagem.Parent := Self;
  FImagem.Center := True;
  FImagem.AutoSize := False;
  FImagem.Left := 1;
  FImagem.Top := 1;
  FImagem.Width := 33;
  FImagem.Height := 33;

  // Criando o label superior
  FLabelSuperior := TLabel.Create(Self);
  FLabelSuperior.Parent := Self;
  FLabelSuperior.Name := 'LabelSuperior';
  FLabelSuperior.Left := 36;
  FLabelSuperior.Top := 4;
  FLabelSuperior.Width := Self.Width - 67;
  FLabelSuperior.Anchors := [akLeft, akTop, akRight];
  FLabelSuperior.AutoSize := False;
  FLabelSuperior.Caption := '';

  // Criando o label inferior
  FLabelInferior := TLabel.Create(Self);
  FLabelInferior.Parent := Self;
  FLabelInferior.Name := 'LabelInferior';
  FLabelInferior.Left := 36;
  FLabelInferior.Top := 18;
  FLabelInferior.Width := Self.Width - 67;
  FLabelInferior.Anchors := [akLeft, akTop, akRight];
  FLabelInferior.AutoSize := False;
  FLabelInferior.Caption := '';

  // Criando um botão para fechar a conversa
  FBotaoFechar := TChatButton.Create(Self);
  FBotaoFechar.Parent := Self;
  FBotaoFechar.Name := 'sdbFechar';
  FBotaoFechar.Caption := 'X';
  FBotaoFechar.Align := alRight;
  FBotaoFechar.AlignWithMargins := True;
  FBotaoFechar.Margins.Right := 4;
  FBotaoFechar.Width := 22;
  FBotaoFechar.TabStop := False;
  FBotaoFechar.ImageAlignment := TImageAlignment.iaCenter;
  FBotaoFechar.OnClick := ppvFechar;

  // Criando uma imagem que vai ficar por cima de todos os componentes do
  // panel, exceto o speedbutton, para que receba todos os eventos de mouse
  FImgMouseMovePanel := TChatImage.Create(Self);
  FImgMouseMovePanel.Parent := Self;
  FImgMouseMovePanel.Align := alClient;
  FImgMouseMovePanel.Name := 'imgAlClient';
  FImgMouseMovePanel.OnClick := pptOnClick;
  FImgMouseMovePanel.OnCMMouseEnter := pptOnCMMouseEnter;
  FImgMouseMovePanel.OnCMMouseLeave := pptOnCMMouseLeave;

  // Ajustando a posição dos componentes no panel
  pptAjustarPosicaoVerticalComponentes;
end;

function TChatPanel.GetFotoThumbnail: TPicture;
begin
  Result := FImagem.Picture;
end;

function TChatPanel.GetLabelInferior: string;
begin
  Result := '';

  if Assigned(FLabelInferior) then
    Result := FLabelInferior.Caption;
end;

function TChatPanel.GetLabelSuperior: string;
begin
  Result := '';

  if Assigned(FLabelSuperior) then
    Result := FLabelSuperior.Caption;
end;

procedure TChatPanel.pptAjustarPosicaoVerticalComponentes;
begin
  // Ajustando a imagem
  if Self.Height <= 35 then
    FImagem.Top := 1
  else
    FImagem.Top := (Self.Height - FImagem.Height) div 2;

  // Ajustando os labels
  if Assigned(FLabelSuperior) and Assigned(FLabelInferior) then
    begin
      if (FLabelSuperior.Caption <> '') and (FLabelInferior.Caption <> '') then
        begin
          FLabelSuperior.Top := (Self.Height - 27) div 2;
          FLabelInferior.Top := FLabelSuperior.Top + FLabelSuperior.Height + 1;
        end
      else
        begin
          if FLabelSuperior.Caption <> '' then
            FLabelSuperior.Top := (Self.Height - 13) div 2
          else
            FLabelInferior.Top := (Self.Height - 13) div 2;
        end;
    end;

  // Ajustando o botão fechar
  if Assigned(FBotaoFechar) then
    begin
      FBotaoFechar.Margins.Top := (Self.Height - 22) div 2;
      FBotaoFechar.Margins.Bottom := Self.Height - FBotaoFechar.Margins.Top - 22;
    end;
end;

procedure TChatPanel.pptOnClick(Sender: TObject);
begin
  Self.Click;
end;

procedure TChatPanel.pptOnCMMouseEnter(Sender: TObject);
begin
  FMouseInControl := True;
  FLabelSuperior.Font.Style := [fsBold, fsItalic];
  FLabelInferior.Font.Style := [fsBold, fsItalic];

  if Assigned(Self.OnMouseEnter) then
    Self.OnMouseEnter(Self);
end;

procedure TChatPanel.pptOnCMMouseLeave(Sender: TObject);
begin
  FMouseInControl := False;
  FLabelSuperior.Font.Style := [];
  FLabelInferior.Font.Style := [];

  if Assigned(Self.OnMouseLeave) then
    Self.OnMouseLeave(Self);
end;

procedure TChatPanel.ppuImagemBotaoFechar(
  const ipCustomImageList: TCustomImageList; const ipIndex: Integer);
begin
  if (ipIndex >= 0) and Assigned(ipCustomImageList) then
    begin
      FBotaoFechar.Images := ipCustomImageList;
      FBotaoFechar.ImageIndex := ipIndex;
      FBotaoFechar.Caption := '';
    end
  else
    begin
      FBotaoFechar.ImageIndex := -1;
      FBotaoFechar.Images := nil;
      FBotaoFechar.Caption := 'X';
    end;
end;

procedure TChatPanel.ppvFechar(Sender: TObject);
begin
  Self.Close;
end;

procedure TChatPanel.SetFotoThumbnail(const Value: TPicture);
begin
  if Assigned(Value) then
    FImagem.Picture.Assign(Value);
end;

procedure TChatPanel.SetIdentificador(const Value: string);
begin
  FIdentificador := Value;
end;

procedure TChatPanel.SetLabelInferior(const Value: string);
begin
  if Assigned(FLabelInferior) then
    FLabelInferior.Caption := Value;

  pptAjustarPosicaoVerticalComponentes;
end;

procedure TChatPanel.SetLabelSuperior(const Value: string);
begin
  if Assigned(FLabelSuperior) then
    FLabelSuperior.Caption := Value;

  pptAjustarPosicaoVerticalComponentes;
end;

procedure TChatPanel.SetOnClose(const Value: TNotifyEvent);
begin
  FOnClose := Value;
end;

procedure TChatPanel.SetPodeFechar(const Value: Boolean);
begin
  if FPodeFechar <> Value then
    begin
      FPodeFechar := Value;

      if Assigned(FBotaoFechar) then
        FBotaoFechar.Visible := FPodeFechar;

      if FBotaoFechar.Visible then
        FLabelSuperior.Width := Self.Width - FLabelSuperior.Left - 25
      else
        FLabelSuperior.Width := Self.Width - FLabelSuperior.Left - 4;

      FLabelInferior.Width := FLabelSuperior.Width;
    end;
end;

procedure TChatPanel.WMSize(var Message: TWMSize);
begin
  inherited;
  pptAjustarPosicaoVerticalComponentes;
end;

{ TChatPanelArquivo }

constructor TChatPanelArquivo.Create(AOwner: TComponent);
begin
  inherited;
  FEnderecoArquivo := '';
  FTipoArquivo := TChatTipoArquivo.taqOutros;
end;

procedure TChatPanelArquivo.SetEnderecoArquivo(const Value: string);
begin
  FEnderecoArquivo := Value;
end;

procedure TChatPanelArquivo.SetTipoArquivo(const Value: TChatTipoArquivo);
begin
  FTipoArquivo := Value;
end;

{ TChatPanelConversa }

constructor TChatPanelConversa.Create(AOwner: TComponent);
begin
  inherited;
  FStatus := 0;

  // Criando o shape status
  FShapeStatus := TShape.Create(Self);
  FShapeStatus.Parent := Self;
  FShapeStatus.Name := 'shpStatus';
  FShapeStatus.Left := 1;
  FShapeStatus.Top := 1;
  FShapeStatus.Height := 33;
  FShapeStatus.Width := 5;
  FShapeStatus.Brush.Color := clGrayText;
  FShapeStatus.Pen.Style := psClear;

  // Ajustando a imagem
  FImagem.Left := 7;

  // Ajustando o label superior
  FLabelSuperior.Left := 42;

  // Ajustando o label inferior
  FLabelInferior.Left := FLabelSuperior.Left;
end;

procedure TChatPanelConversa.pptAjustarPosicaoVerticalComponentes;
begin
  inherited;

  // Ajustando o shape do status e a imagem
  if Assigned(FShapeStatus) and Assigned(FImagem) then
    begin
      FShapeStatus.Top := (Self.Height - FShapeStatus.Height) div 2;
      FImagem.Top := FShapeStatus.Top;
    end;
end;

procedure TChatPanelConversa.SetStatus(const Value: Integer);
begin
  if Assigned(FShapeStatus) then
    begin
      FShapeStatus.Visible := (Value >= 0) and (Value <= 2);

      case Value of
        0: // Desconectado
          begin
            FStatus := 0;
            FShapeStatus.Brush.Color := clGrayText;
          end;
        1: // Conectado
          begin
            FStatus := 1;
            FShapeStatus.Brush.Color := clGreen;
          end;
        2: // Ausente
          begin
            FStatus := 2;
            FShapeStatus.Brush.Color := $0001C8EB;
          end;
      end;
    end;
end;

{ TChatPanelConversaAba }

procedure TChatPanelConversaAba.Click;
begin
  inherited;
  Self.Selecionado := True;
end;

constructor TChatPanelConversaAba.Create(AOwner: TComponent);
begin
  inherited;

  FMensagem := TChatMensagem.Create;
  FNotificacao := False;
  FNotificacaoTimer := nil;
  FNotificacaoTimerCount := 0;
  FOnChangeSelect := nil;
  FPodeSelecionar := True;
  FSelecionado := False;
  FTipoAba := TChatTipoAba.taConfiguracao;
end;

destructor TChatPanelConversaAba.Destroy;
begin
  if Assigned(FMensagem) then
    FreeAndNil(FMensagem);

  if Assigned(FNotificacaoTimer) then
    begin
      FNotificacaoTimer.Enabled := False;
      FreeAndNil(FNotificacaoTimer);
    end;

  inherited;
end;

procedure TChatPanelConversaAba.pptOnCMMouseLeave(Sender: TObject);
begin
  inherited;

  if Selecionado or Notificacao then
    begin
      FLabelSuperior.Font.Style := [fsBold];
      FLabelInferior.Font.Style := [fsBold];
    end;
end;

procedure TChatPanelConversaAba.ppvNotificacaoOnTimer(Sender: TObject);
begin
  if FNotificacaoTimerCount < 0 then
    FNotificacaoTimerCount := 0
  else
    begin
      if FNotificacaoTimerCount > 1 then
        FNotificacaoTimerCount := 0;
    end;

  case FNotificacaoTimerCount of
    0:
      begin
        FLabelSuperior.Font.Color := clWhite;
        Self.Color := clRed;
      end;
    1:
      begin
        FLabelSuperior.Font.Color := clWindowText;
        Self.Color := clBtnFace;
      end;
  end;

  FLabelInferior.Font.Color := FLabelSuperior.Font.Color;
  Inc(FNotificacaoTimerCount);
end;

procedure TChatPanelConversaAba.SetNotificacao(const Value: Boolean);
var
  vaPiscar: Boolean;
begin
  vaPiscar := FNotificacao <> Value;
  FNotificacao := Value and (not FSelecionado);

  if FNotificacao then
    begin
      if vaPiscar then
        begin
          if MouseInControl then
            FLabelSuperior.Font.Style := [fsBold, fsItalic]
          else
            FLabelSuperior.Font.Style := [fsBold];

          FLabelInferior.Font := FLabelSuperior.Font;

          if not Assigned(FNotificacaoTimer) then
            FNotificacaoTimer := TTimer.Create(Self);

          FNotificacaoTimer.Enabled := False;
          FNotificacaoTimerCount := 0;
          FNotificacaoTimer.Interval := 500;
          FNotificacaoTimer.OnTimer := ppvNotificacaoOnTimer;
          FNotificacaoTimer.Enabled := True;
        end;
    end
  else
    begin
      if Assigned(FNotificacaoTimer) then
        begin
          FNotificacaoTimer.Enabled := False;
          FreeAndNil(FNotificacaoTimer);
        end;

      if FSelecionado then
        begin
          FLabelSuperior.Font.Color := clWhite;

          if MouseInControl then
            FLabelSuperior.Font.Style := [fsBold, fsItalic]
          else
            FLabelSuperior.Font.Style := [fsBold];
        end
      else
        begin
          FLabelSuperior.Font.Color := clWindowText;

          if MouseInControl then
            FLabelSuperior.Font.Style := [fsBold, fsItalic]
          else
            FLabelSuperior.Font.Style := [];
        end;

      FLabelInferior.Font := FLabelSuperior.Font;
      Self.Color := clBtnFace;
    end;
end;

procedure TChatPanelConversaAba.SetOnChangeSelect(const Value: TNotifyEvent);
begin
  FOnChangeSelect := Value;
end;

procedure TChatPanelConversaAba.SetPodeSelecionar(const Value: Boolean);
begin
  if (not Value) and FPodeSelecionar then
    Self.Selecionado := False;

  FPodeSelecionar := Value;
end;

procedure TChatPanelConversaAba.SetSelecionado(const Value: Boolean);
begin
  if FSelecionado <> Value then
    begin
      FSelecionado := FPodeSelecionar and Value;

      if FSelecionado then
        begin
          if FNotificacao then
            Notificacao := False;

          Self.Color := clSilver;
          Self.BevelOuter := bvNone;
          FLabelSuperior.Font.Color := clWhite;

          if MouseInControl then
            FLabelSuperior.Font.Style := [fsBold, fsItalic]
          else
            FLabelSuperior.Font.Style := [fsBold];
        end
      else
        begin
          Self.Color := clBtnFace;
          Self.BevelOuter := bvRaised;
          FLabelSuperior.Font.Color := clWindowText;

          if MouseInControl then
            FLabelSuperior.Font.Style := [fsBold, fsItalic]
          else
            FLabelSuperior.Font.Style := [];
        end;

       // Atribuindo ao label inferior a mesma configuração de fonte do label superior
       FLabelInferior.Font := FLabelSuperior.Font;

      if FPodeSelecionar and Assigned(FOnChangeSelect) then
        FOnChangeSelect(Self);
    end;
end;

procedure TChatPanelConversaAba.SetTipoAba(const Value: TChatTipoAba);
begin
  FTipoAba := Value;
end;

end.
